import org.gradle.kotlin.dsl.ProjectExtensionsKt

plugins {
  id 'groovy'
  id 'java-gradle-plugin'
  id "org.jetbrains.kotlin.jvm" version "1.3.61"
  id "com.gradle.plugin-publish" version "0.11.0"
}

group = 'org.jetbrains.gradle.plugin.idea-ext'
version = '1.1.11-SNAPSHOT'

repositories {
  mavenCentral()
}

dependencies {
  implementation 'com.google.code.gson:gson:2.10.1'
  implementation ProjectExtensionsKt.gradleKotlinDsl(project)

  testImplementation 'org.jetbrains.kotlin:kotlin-stdlib'
  testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
    exclude group: 'org.codehaus.groovy'
  }
  testImplementation "org.assertj:assertj-core:3.15.0"
}

gradlePlugin {
  plugins {
    ideaExtPlugin {
      id = "org.jetbrains.gradle.plugin.idea-ext"
      implementationClass = "org.jetbrains.gradle.ext.IdeaExtPlugin"
    }
  }
}

pluginBundle {
  website = 'https://github.com/jetbrains/gradle-idea-ext-plugin'
  vcsUrl = 'https://github.com/jetbrains/gradle-idea-ext-plugin'

  description = 'Extends the Gradle\'s "idea" DSL with specific settings: code style, facets, run configurations etc.'
  tags = ['intellij', 'idea', 'settings']

  plugins {
    ideaExtPlugin {
      id = 'org.jetbrains.gradle.plugin.idea-ext'
      displayName = 'Gradle Idea Extension plugin'
    }
  }
}

tasks.register("testJdk17", Test) {
    group = "verification"
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(17)
    }
    jvmArgs('--add-opens=java.base/java.lang=ALL-UNNAMED') // workaround for https://github.com/gradle/gradle/issues/22317
}

check.dependsOn(testJdk17)


wrapper {
  gradleVersion = "5.6.4"
  distributionType = Wrapper.DistributionType.BIN
}

tasks.named('compileGroovy') {
  // do not depend on compileJava
  classpath = sourceSets.main.compileClasspath
}
// depend on compileGroovy
compileKotlin.classpath += files(sourceSets.main.groovy.classesDirectory)
compileKotlin.kotlinOptions.jvmTarget=1.8
